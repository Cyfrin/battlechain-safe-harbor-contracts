// SPDX-License-Identifier: MIT
pragma solidity 0.8.34;

import { CreateX } from "src/CreateX.sol";
import { IAttackRegistry } from "src/interface/IAttackRegistry.sol";

/// @title BattleChainDeployer
/// @notice Extends CreateX to automatically register deployments with AttackRegistry
/// @dev All deployments through this contract will be recorded for BattleChain attack mode eligibility
/// @dev The deploy/compute surface is mirrored in IBattleChainDeployer for downstream consumers.
///      The contract does NOT inherit that interface: CreateX already implements this surface, and
///      its compute* functions are non-virtual, so they cannot be overridden to satisfy a second
///      interface declaration (and CreateX.Values would clash with the interface's Values). Keep
///      IBattleChainDeployer in sync manually if this surface changes.
contract BattleChainDeployer is CreateX {
    /*//////////////////////////////////////////////////////////////
                                 ERRORS
    //////////////////////////////////////////////////////////////*/
    error BattleChainDeployer__ZeroAddress();

    /*//////////////////////////////////////////////////////////////
                            STATE VARIABLES
    //////////////////////////////////////////////////////////////*/
    /// @dev The AttackRegistry address
    IAttackRegistry public immutable ATTACK_REGISTRY;

    /*//////////////////////////////////////////////////////////////
                              CONSTRUCTOR
    //////////////////////////////////////////////////////////////*/
    /// @param attackRegistry The AttackRegistry address
    constructor(address attackRegistry) {
        if (attackRegistry == address(0)) {
            revert BattleChainDeployer__ZeroAddress();
        }
        ATTACK_REGISTRY = IAttackRegistry(attackRegistry);
    }

    /*//////////////////////////////////////////////////////////////
                        INTERNAL REGISTRATION HOOK
    //////////////////////////////////////////////////////////////*/
    /// @dev Registers the deployed contract with AttackRegistry
    function _registerDeployment(address newContract) internal {
        ATTACK_REGISTRY.registerDeployment(newContract, msg.sender);
    }

    /*//////////////////////////////////////////////////////////////
                           CREATE OVERRIDES
    //////////////////////////////////////////////////////////////*/
    /// @inheritdoc CreateX
    function deployCreate(bytes memory initCode) public payable override returns (address newContract) {
        newContract = super.deployCreate(initCode);
        _registerDeployment(newContract);
    }

    /// @inheritdoc CreateX
    function deployCreateAndInit(
        bytes memory initCode,
        bytes memory data,
        Values memory values,
        address refundAddress
    )
        public
        payable
        override
        returns (address newContract)
    {
        newContract = super.deployCreateAndInit(initCode, data, values, refundAddress);
        _registerDeployment(newContract);
    }

    /// @inheritdoc CreateX
    /// @dev Registration handled by deployCreateAndInit(initCode, data, values, refundAddress)
    function deployCreateAndInit(
        bytes memory initCode,
        bytes memory data,
        Values memory values
    )
        public
        payable
        override
        returns (address newContract)
    {
        newContract = super.deployCreateAndInit(initCode, data, values);
    }

    /// @inheritdoc CreateX
    function deployCreateClone(
        address implementation,
        bytes memory data
    )
        public
        payable
        override
        returns (address proxy)
    {
        proxy = super.deployCreateClone(implementation, data);
        _registerDeployment(proxy);
    }

    /*//////////////////////////////////////////////////////////////
                          CREATE2 OVERRIDES
    //////////////////////////////////////////////////////////////*/
    /// @inheritdoc CreateX
    function deployCreate2(bytes32 salt, bytes memory initCode) public payable override returns (address newContract) {
        newContract = super.deployCreate2(salt, initCode);
        _registerDeployment(newContract);
    }

    /// @inheritdoc CreateX
    /// @dev Registration handled by deployCreate2(salt, initCode)
    function deployCreate2(bytes memory initCode) public payable override returns (address newContract) {
        newContract = super.deployCreate2(initCode);
    }

    /// @inheritdoc CreateX
    function deployCreate2AndInit(
        bytes32 salt,
        bytes memory initCode,
        bytes memory data,
        Values memory values,
        address refundAddress
    )
        public
        payable
        override
        returns (address newContract)
    {
        newContract = super.deployCreate2AndInit(salt, initCode, data, values, refundAddress);
        _registerDeployment(newContract);
    }

    /// @inheritdoc CreateX
    /// @dev Registration handled by deployCreate2AndInit(salt, initCode, data, values, refundAddress)
    function deployCreate2AndInit(
        bytes32 salt,
        bytes memory initCode,
        bytes memory data,
        Values memory values
    )
        public
        payable
        override
        returns (address newContract)
    {
        newContract = super.deployCreate2AndInit(salt, initCode, data, values);
    }

    /// @inheritdoc CreateX
    /// @dev Registration handled by deployCreate2AndInit(salt, initCode, data, values, refundAddress)
    function deployCreate2AndInit(
        bytes memory initCode,
        bytes memory data,
        Values memory values,
        address refundAddress
    )
        public
        payable
        override
        returns (address newContract)
    {
        newContract = super.deployCreate2AndInit(initCode, data, values, refundAddress);
    }

    /// @inheritdoc CreateX
    /// @dev Registration handled by deployCreate2AndInit(salt, initCode, data, values, refundAddress)
    function deployCreate2AndInit(
        bytes memory initCode,
        bytes memory data,
        Values memory values
    )
        public
        payable
        override
        returns (address newContract)
    {
        newContract = super.deployCreate2AndInit(initCode, data, values);
    }

    /// @inheritdoc CreateX
    function deployCreate2Clone(
        bytes32 salt,
        address implementation,
        bytes memory data
    )
        public
        payable
        override
        returns (address proxy)
    {
        proxy = super.deployCreate2Clone(salt, implementation, data);
        _registerDeployment(proxy);
    }

    /// @inheritdoc CreateX
    /// @dev Registration handled by deployCreate2Clone(salt, implementation, data)
    function deployCreate2Clone(
        address implementation,
        bytes memory data
    )
        public
        payable
        override
        returns (address proxy)
    {
        proxy = super.deployCreate2Clone(implementation, data);
    }

    /*//////////////////////////////////////////////////////////////
                          CREATE3 OVERRIDES
    //////////////////////////////////////////////////////////////*/
    /// @inheritdoc CreateX
    function deployCreate3(bytes32 salt, bytes memory initCode) public payable override returns (address newContract) {
        newContract = super.deployCreate3(salt, initCode);
        _registerDeployment(newContract);
    }

    /// @inheritdoc CreateX
    /// @dev Registration handled by deployCreate3(salt, initCode)
    function deployCreate3(bytes memory initCode) public payable override returns (address newContract) {
        newContract = super.deployCreate3(initCode);
    }

    /// @inheritdoc CreateX
    function deployCreate3AndInit(
        bytes32 salt,
        bytes memory initCode,
        bytes memory data,
        Values memory values,
        address refundAddress
    )
        public
        payable
        override
        returns (address newContract)
    {
        newContract = super.deployCreate3AndInit(salt, initCode, data, values, refundAddress);
        _registerDeployment(newContract);
    }

    /// @inheritdoc CreateX
    /// @dev Registration handled by deployCreate3AndInit(salt, initCode, data, values, refundAddress)
    function deployCreate3AndInit(
        bytes32 salt,
        bytes memory initCode,
        bytes memory data,
        Values memory values
    )
        public
        payable
        override
        returns (address newContract)
    {
        newContract = super.deployCreate3AndInit(salt, initCode, data, values);
    }

    /// @inheritdoc CreateX
    /// @dev Registration handled by deployCreate3AndInit(salt, initCode, data, values, refundAddress)
    function deployCreate3AndInit(
        bytes memory initCode,
        bytes memory data,
        Values memory values,
        address refundAddress
    )
        public
        payable
        override
        returns (address newContract)
    {
        newContract = super.deployCreate3AndInit(initCode, data, values, refundAddress);
    }

    /// @inheritdoc CreateX
    /// @dev Registration handled by deployCreate3AndInit(salt, initCode, data, values, refundAddress)
    function deployCreate3AndInit(
        bytes memory initCode,
        bytes memory data,
        Values memory values
    )
        public
        payable
        override
        returns (address newContract)
    {
        newContract = super.deployCreate3AndInit(initCode, data, values);
    }
}
